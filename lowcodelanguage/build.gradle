import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.licenser'
    id 'net.minecraftforge.gradleutils'
}

apply from: rootProject.file('build_shared.gradle')

java {
    toolchain.languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
    withSourcesJar()
}

dependencies {
    compileOnly(libs.nulls)

    implementation(project(':fmlloader'))
    implementation(project(':fmlcore'))
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Automatic-Module-Name': 'net.minecraftforge.lowcodemod',
                'FMLModType': 'LANGPROVIDER'
        ] as LinkedHashMap)
        attributes([
                'Specification-Title':   'LowCodeMod',
                'Specification-Vendor':  'Forge Development LLC',
                'Specification-Version': '1.0',
                'Implementation-Title':   project.name,
                'Implementation-Vendor': 'Forge Development LLC',
                'Implementation-Version': FORGE_VERSION.split('\\.')[0]
        ] as java.util.LinkedHashMap, 'net/minecraftforge/fml/lowcodemod/')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:unchecked'
}

license {
    header = rootProject.file('LICENSE-header.txt')
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        from components.java
        artifactId = 'lowcodelanguage'
        pom {
            name = project.name
            description = 'Language provider for Ketting that loads resource packs as mods'
            url = "https://github.com/kettingpowered/Ketting-1-20-x"
            developers {
                developer {
                    id = "justred23"
                    name = "JustRed23"
                }
                developer {
                    id = "c0d3m4513r"
                    name = "C0D3 M4513R"
                }
            }
            scm {
                connection = "scm:git:https://github.com/kettingpowered/Ketting-1-20-x.git"
                //developerConnection = "scm:svn:https://subversion.example.com/svn/project/trunk/"
                url = "https://github.com/kettingpowered/Ketting-1-20-x"
            }
            PomUtils.setGitHubDetails(pom, 'kettingpowered', 'Ketting-1-20-x')
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kettingpowered/Ketting-1-20-x")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = 'kettingRepo'
            credentials {
                username = System.getenv("KETTINGUSERNAME")
                password = System.getenv("KETTINGPASSWORD")
            }
            url = "https://nexus.c0d3m4513r.com/repository/Ketting-Server-Releases/"
        }
    }
}